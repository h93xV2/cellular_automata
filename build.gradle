plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'jacoco'
    id 'org.beryx.jlink' version '3.1.5'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

javafx {
    version = "25.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jacoco {
    toolVersion = "0.8.14"
}

dependencies {
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'

    testImplementation(platform('org.junit:junit-bom:6.0.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

application {
    mainClass = 'cellular_automata.CellularAutomataApp'
    applicationDefaultJvmArgs = ["--enable-native-access=javafx.graphics,ALL-UNNAMED"]
    mainModule = 'cellular_automata'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        csv.required = true
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'CellularAutomata'
    }
    jpackage {
        imageName = 'CellularAutomata'
        installerName = 'CellularAutomata'

        appVersion = '1.0.0'
        vendor = 'h93xV2'

        // macOS
        installerType = 'dmg'

        def macId = "io.github.h93xv2.cellularautomata"
        imageOptions = ["--mac-package-identifier", macId]
        installerOptions = ["--mac-package-identifier", macId]
    }
}